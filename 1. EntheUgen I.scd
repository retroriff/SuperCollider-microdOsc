/**
_____________________________________________________________________________/\\\________/\\\\\_________________________________
 ____________________________________________________________________________\/\\\______/\\\///\\\_______________________________
  ______________________/\\\__________________________________________________\/\\\____/\\\/__\///\\\_____________________________
   ____/\\\\\__/\\\\\___\///______/\\\\\\\\__/\\/\\\\\\\______/\\\\\___________\/\\\___/\\\______\//\\\__/\\\\\\\\\\_____/\\\\\\\\_
    __/\\\///\\\\\///\\\__/\\\___/\\\//////__\/\\\/////\\\___/\\\///\\\____/\\\\\\\\\__\/\\\_______\/\\\_\/\\\//////____/\\\//////__
     _\/\\\_\//\\\__\/\\\_\/\\\__/\\\_________\/\\\___\///___/\\\__\//\\\__/\\\////\\\__\//\\\______/\\\__\/\\\\\\\\\\__/\\\_________
      _\/\\\__\/\\\__\/\\\_\/\\\_\//\\\________\/\\\_________\//\\\__/\\\__\/\\\__\/\\\___\///\\\__/\\\____\////////\\\_\//\\\________
       _\/\\\__\/\\\__\/\\\_\/\\\__\///\\\\\\\\_\/\\\__________\///\\\\\/___\//\\\\\\\/\\____\///\\\\\/______/\\\\\\\\\\__\///\\\\\\\\_
        _\///___\///___\///__\///_____\////////__\///_____________\/////______\///////\//_______\/////_______\//////////_____\////////__

* @date: May, 2021
*/

(
// Start Proxy
p = ProxySpace(s).push;
p[\tempo] = 110 / 60;
p.quant = 4;
p.fadeTime = 20;
StageLimiter.new(2);
)

(
// Panes
s.plotTree;
s.scope(2);
s.makeWindow;
m = ProxyMixer(p, 8);
m.parent.alwaysOnTop_(true);
PdefAllGui.new;
)

(
~bass = {
    var lfo1 = LFPulse.ar(~tempo.kr / 2).range(0.5, 1);
    var lfo2 = SinOscFB.ar(~tempo.kr * 8).range(0.5, 1);

    SinOscFB.ar(
        freq: [200, 210] * lfo1 * lfo2,
        feedback: SinOsc.kr(~tempo.kr / 16, 0).max(0),
        mul: 1
    ).tanh;
}
)

(
~gaza = {
    Mix.fill(10, {
        Ringz.ar(
            in: WhiteNoise.ar(0.001)!2,
            freq: XLine.kr(
                start: exprand(100.0, 5000.0),
                end: exprand(100.0, 5000.0),
                dur: 60
            ),
            decaytime: 0.6
        );
    });
};
~gaza.fadeTime = 60;
)

(
~beat = {
    var lfo = Line.kr(start: 400, end: 600, dur: 200);
    var freq1 = Trig.ar(LFPulse.ar(~tempo.kr / 4), dur: 0.2).lag;
    var freq2 = SinOscFB.ar([1, 1.1, 1.05] * lfo, feedback: 0.5, mul: 0.2);

    CombC.ar(
        in: freq1 * freq2,
        maxdelaytime: ~tempo.kr * 4,
        delaytime: ~tempo.kr / 8,
        decaytime: ~tempo.kr
    );
}
)

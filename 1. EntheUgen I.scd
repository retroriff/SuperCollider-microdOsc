/**
_____________________________________________________________________________/\\\________/\\\\\_________________________________
 ____________________________________________________________________________\/\\\______/\\\///\\\_______________________________
  ______________________/\\\__________________________________________________\/\\\____/\\\/__\///\\\_____________________________
   ____/\\\\\__/\\\\\___\///______/\\\\\\\\__/\\/\\\\\\\______/\\\\\___________\/\\\___/\\\______\//\\\__/\\\\\\\\\\_____/\\\\\\\\_
    __/\\\///\\\\\///\\\__/\\\___/\\\//////__\/\\\/////\\\___/\\\///\\\____/\\\\\\\\\__\/\\\_______\/\\\_\/\\\//////____/\\\//////__
     _\/\\\_\//\\\__\/\\\_\/\\\__/\\\_________\/\\\___\///___/\\\__\//\\\__/\\\////\\\__\//\\\______/\\\__\/\\\\\\\\\\__/\\\_________
      _\/\\\__\/\\\__\/\\\_\/\\\_\//\\\________\/\\\_________\//\\\__/\\\__\/\\\__\/\\\___\///\\\__/\\\____\////////\\\_\//\\\________
       _\/\\\__\/\\\__\/\\\_\/\\\__\///\\\\\\\\_\/\\\__________\///\\\\\/___\//\\\\\\\/\\____\///\\\\\/______/\\\\\\\\\\__\///\\\\\\\\_
        _\///___\///___\///__\///_____\////////__\///_____________\/////______\///////\//_______\/////_______\//////////_____\////////__
*/

~m11 = Ndef(\m11).play;
~m11.fadeTime = 10;

(
~m11[0] = {
    var lfo1 = LFPulse.ar(t / 2).range(0.5, 1);
    var lfo2 = SinOscFB.ar(t * 8).range(0.5, 1);

    SinOscFB.ar(
        freq: [55, 56].midicps * lfo1 * lfo2,
        feedback: SinOsc.kr(t / 16, 0).max(0),
        mul: 1
    ).tanh;
};
)

~m12 = Ndef(\m12).vol_(0.3).play;
~m12.fadeTime = 10;

(
~m12[0] = {
    Mix.fill(10, {
        Ringz.ar(
            in: WhiteNoise.ar(0.001)!2,
            freq: XLine.kr(
                start: exprand(100.0, 5000.0),
                end: exprand(100.0, 5000.0),
                dur: 60
            ),
            decaytime: 0.6
        );
    });
};
)

~m13 = Ndef(\m13).play;
~m13.fadeTime = 0;

(
~m13[0] = {
    var lfo = Line.kr(
        start: 400,
        end: 600,
        dur: 200
    );

    var freq = SinOscFB.ar(
        freq: [1, 1.1, 1.05] * lfo,
        feedback: 0.5,
        mul: 0.2
    );

    var trig = Trig.ar(LFPulse.ar(t / 4), dur: 0.2).lag;

    CombC.ar(
        in: freq * trig,
        maxdelaytime: t * 4,
        delaytime: t / 8,
        decaytime: t
    );
};
)
